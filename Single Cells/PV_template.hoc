/////////////////////
// Model Templates //
/////////////////////

begintemplate InterneuronCell
	public soma, dend, connect2target, type,connect_gap,LFPtemp_xtra,drv
	create soma, dend

	public ncl, synlist
	objref ncl, synlist,gaps[200],drv
	strdef type

	objref tlist, area_vec
   public tlist, area_vec
   external tstop
	public soma_scl, dend_scl, soma_cap_scl, dend_cap_scl

	proc init() {	

	 tlist = new List()
    area_vec = new Vector()	
	
		synlist = new List()
		ncl = new List()
		type = "InterneuronCell"

		soma_scl = 2.158
		dend_scl = 2.158
		soma_cap_scl = .825
		dend_cap_scl = .825
		/////// topology ////////
		create soma, dend
		connect dend(0), soma(1)

		/////// geometry ////////
		soma {
			nseg=1
			L=15*soma_scl		// (micrometer)
			diam=15	*soma_scl	// (micrometer)
	    	}

		dend {
			nseg=1
			L=150*dend_scl		// (micrometer)
			diam=10*dend_scl		// (micrometer)
		}

		/////// biophysics //////
		soma {
			cm = 1*soma_cap_scl		    // (microF/cm2)
			Ra = 150*22.5	// (ohm-cm)
			Rm = 20000	    // (ohm-cm2)
			
			insert leakinter
			glbar_inter_leakinter = 1/Rm // (siemens/cm2)
			el_leakinter = -70	// (mV)

			insert nainter
			insert kdrinter
	    	}

		dend {
			cm = 1*dend_cap_scl		// (microF/cm2)
			Ra = 150	// (ohm-cm)
			Rm = 20000	// (ohm-cm2)
			
			insert leakinter
			glbar_inter_leakinter = 1/Rm // (siemens/cm2)
			el_leakinter = -70	// (mV)

			insert nainter
			insert kdrinter
		}
		soma {
			ena = 45		// (mV)
			gnabar_nainter = 0.035	// (siemens/cm2)

			ek = -80		// (mV)
			gkdrbar_kdrinter = 0.008 // (siemens/cm2) 
			
		}

		dend {
			ena = 45		// (mV)
			gnabar_nainter = 0.010	// (siemens/cm2)

			ek = -80		// (mV)
			gkdrbar_kdrinter = 0.003 // (siemens/cm2)
			
			//insert ca_ion
			//eca = 120
		}
	}
	

obfunc connect2target() { localobj nc
	soma nc = new NetCon(&v(0.5), $o1)
	nc.threshold = 0
	if (numarg() == 2) ($o2 = nc)
	return nc
	}
endtemplate InterneuronCell
